plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.139'
    id 'idea'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()

            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation "maven.modrinth:ae2:19.2.17"
    implementation "maven.modrinth:guideme:21.1.15"
    implementation(jarJar("io.github.llamalad7:mixinextras-neoforge:0.5.3"))

    compileOnly "maven.modrinth:applied-energistics-2-wireless-terminals:19.3.0"
    compileOnly "maven.modrinth:advancedae:1.6.9-1.21.1"

    // END OF REQUIRED

    implementation "maven.modrinth:jade:15.10.4+neoforge"
    implementation "maven.modrinth:jei:YAcQ6elZ"
    implementation "maven.modrinth:emi:1.1.22+1.21.1+neoforge"
    implementation "maven.modrinth:mekanism:10.7.17.83"
    implementation "maven.modrinth:mekanism-generators:10.7.17.83"
    implementation "maven.modrinth:extended-ae:1.21-2.2.27-neoforge"
    implementation "maven.modrinth:glodium:1.21-2.2-neoforge"
    implementation "curse.maven:flux-networks-248020:6089446"
    implementation "maven.modrinth:journeymap:1.21.1-6.0.0-beta.53+neoforge"
    implementation "curse.maven:architectury-api-419699:5786327"
    implementation "curse.maven:ftb-chunks-forge-314906:7157142"
    implementation "curse.maven:ftb-library-forge-404465:7420412"
    implementation "curse.maven:ftb-teams-forge-404468:7369021"
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}